// TODO fix dependencies in all projects, use minimal set of dependencies etc.
plugins {
  id "fr.brouillard.oss.gradle.jgitver" version "$gradleJgitverPluginVersion"
}

jgitver {
  mavenLike true
  policy {
    pattern = "(.*)"
    transformations = ["IGNORE"]
  }
}

allprojects {
  repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://repository.dmz.croz.net/repository/releases/" }
    maven { url "https://repository.dmz.croz.net/repository/snapshots/" }
  }

  configurations {
    compileOnly.extendsFrom(annotationProcessor)

    springBom
    compileOnly.extendsFrom(springBom)
    annotationProcessor.extendsFrom(springBom)
    implementation.extendsFrom(springBom)
    testAnnotationProcessor.extendsFrom(springBom)
  }
}

subprojects {
  apply plugin: "jacoco"
  apply plugin: "java"
  apply plugin: "java-library"
  apply plugin: "maven-publish"

  group = "net.croz.nrich"

  sourceCompatibility = "1.8"

  java {
    ["envers", "reactor", "web", "webMvc", "webFlux"].forEach {
      registerFeature(it) {
        usingSourceSet(sourceSets.main)
      }
    }

    withSourcesJar()
  }

  dependencies {
    springBom platform("org.springframework.boot:spring-boot-dependencies:$springBootVersion")

    constraints {
      implementation "org.apache.poi:poi:$apachePoiVersion"
      implementation "org.apache.poi:poi-ooxml:$apachePoiVersion"
      implementation "org.modelmapper:modelmapper:$modelMapperVersion"
    }
  }

  test {
    useJUnitPlatform()
    jvmArgs = ["-noverify", "-XX:TieredStopAtLevel=1"]
  }

  publishing {
    repositories {
      maven {
        credentials {
          username System.getenv("GRC_GRADLE_USERNAME")
          password System.getenv("GRC_GRADLE_PASSWORD")
        }

        String releasesRepoUrl = "https://repository.dmz.croz.net/repository/releases/"
        String snapshotsRepoUrl = "https://repository.dmz.croz.net/repository/snapshots/"

        url = version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
      }
    }

    publications {
      mavenJava(MavenPublication) {
        from components.java
        suppressAllPomMetadataWarnings()
      }
    }
  }
}
