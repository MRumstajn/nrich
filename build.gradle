// TODO fix dependencies in all projects, use minimal set of dependencies etc.
plugins {
  id "fr.brouillard.oss.gradle.jgitver" version "$gradleJgitverPluginVersion"
  id "io.github.gradle-nexus.publish-plugin" version "$gradlePublishPluginVersion"
}

jgitver {
  mavenLike true
  policy {
    pattern = "(.*)"
    transformations = ["IGNORE"]
  }
}

allprojects {
  repositories {
    mavenLocal()
    mavenCentral()
  }

  configurations {
    compileOnly.extendsFrom(annotationProcessor)

    springBom
    compileOnly.extendsFrom(springBom)
    annotationProcessor.extendsFrom(springBom)
    implementation.extendsFrom(springBom)
    testAnnotationProcessor.extendsFrom(springBom)
  }
}

subprojects {
  boolean isPlatformProject = it.name.endsWith("bom")

  apply plugin: "maven-publish"
  apply plugin: "signing"

  if (isPlatformProject) {
    apply plugin: "java-platform"
  }
  else {
    apply plugin: "jacoco"
    apply plugin: "java"
    apply plugin: "java-library"

    sourceCompatibility = "1.8"

    java {
      ["envers", "reactor", "web", "webMvc", "webFlux"].forEach {
        registerFeature(it) {
          usingSourceSet(sourceSets.main)
        }
      }

      withSourcesJar()
      withJavadocJar()
    }

    dependencies {
      springBom platform("org.springframework.boot:spring-boot-dependencies:$springBootVersion")

      constraints {
        implementation "org.apache.poi:poi:$apachePoiVersion"
        implementation "org.apache.poi:poi-ooxml:$apachePoiVersion"
        implementation "org.modelmapper:modelmapper:$modelMapperVersion"
      }
    }

    test {
      useJUnitPlatform()
      jvmArgs = ["-noverify", "-XX:TieredStopAtLevel=1"]
    }

    tasks.withType(JavaCompile) {
      options.encoding = "UTF-8"

    }
  }

  group = "net.croz.nrich"

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from isPlatformProject ? components.javaPlatform : components.java

        suppressAllPomMetadataWarnings()

        pom {
          name = getProject().getName()
          description = "$projectDescription"
          url = "$projectRepositoryUrl"

          licenses {
            license {
              name = "$licenseName"
              url = "$licenseUrl"
            }
          }

          developers {
            developer {
              name = "$developerName"
              email = "$developerEmail"
              organization = "$developerOrganization"
              organizationUrl = "$developerOrganizationUrl"
            }
          }

          String gitRepositoryUrl = "scm:git:${projectRepositoryUrl}.git"
          scm {
            connection = gitRepositoryUrl
            developerConnection = gitRepositoryUrl
            url = "$projectRepositoryUrl"
          }
        }
      }
    }
  }

  signing {
    // The gpg private key needs to be stored in ascii-armored format
    useInMemoryPgpKeys(System.getenv("GPG_PRIVATE_KEY"), System.getenv("GPG_PASSPHRASE"))
    sign publishing.publications.mavenJava
  }

  tasks.withType(Sign) {
    onlyIf {
      System.getenv("GPG_PRIVATE_KEY") && System.getenv("GPG_PASSPHRASE")
    }
  }
}

nexusPublishing {
  repositories {
    sonatype {
      nexusUrl = uri("$sonatypeNexusUrl/service/local/")
      snapshotRepositoryUrl = uri("$sonatypeNexusUrl/content/repositories/snapshots/")

      username = "$sonatypeUsername"
      password = System.getenv("SONATYPE_PASSWORD")

      // Specifying the configuration property explicitly rather than relying on the API call
      // is considered a performance optimization and can reduce execution time by several seconds
      stagingProfileId = System.getenv("SONATYPE_STAGING_PROFILE_ID")
    }
  }
}
